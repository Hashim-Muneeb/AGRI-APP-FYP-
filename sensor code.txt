// Include required libraries
#include <ESP8266WiFi.h>
#include <Firebase_ESP_Client.h>
#include <addons/TokenHelper.h>
#include <DHT.h>  // Include the DHT sensor library

// Define DHT sensor parameters
#define DHTPIN D4
#define DHTTYPE DHT11

// Define Soil Moisture sensor pin
#define SOIL_MOISTURE_PIN A0

// Define WiFi credentials
#define WIFI_SSID "HASHIM"
#define WIFI_PASSWORD "876543210"

// Define Firebase API Key, Project ID, and user credentials
#define API_KEY "AIzaSyBqA1RN0QqbdjqA4vsaPCfKqlEcJiiBDEg"
#define FIREBASE_PROJECT_ID "agriappfyp"
#define USER_EMAIL "hashim12@gmail.com"
#define USER_PASSWORD "hashim123"

// Define Firebase Data object, Firebase authentication, and configuration
FirebaseData fbdo;
FirebaseAuth auth;
FirebaseConfig config;

// Initialize the DHT sensor
DHT dht(DHTPIN, DHTTYPE);

void setup() {
  // Initialize serial communication for debugging
  Serial.begin(115200);

  // Initialize the DHT sensor
  dht.begin();

  // Connect to Wi-Fi
  WiFi.begin(WIFI_SSID, WIFI_PASSWORD);
  Serial.print("Connecting to Wi-Fi");
  while (WiFi.status() != WL_CONNECTED) {
    Serial.print(".");
    delay(300);
  }
  Serial.println();
  Serial.print("Connected with IP: ");
  Serial.println(WiFi.localIP());
  Serial.println();

  // Print Firebase client version
  Serial.printf("Firebase Client v%s\n\n", FIREBASE_CLIENT_VERSION);

  // Assign the API key
  config.api_key = API_KEY;

  // Assign the user sign-in credentials
  auth.user.email = USER_EMAIL;
  auth.user.password = USER_PASSWORD;

  // Assign the callback function for the long-running token generation task
  config.token_status_callback = tokenStatusCallback;  // see addons/TokenHelper.h

  // Begin Firebase with configuration and authentication
  Firebase.begin(&config, &auth);

  // Reconnect to Wi-Fi if necessary
  Firebase.reconnectWiFi(true);
}

void loop() {
  // Read temperature and humidity from the DHT sensor
  float temperature = dht.readTemperature();
  float humidity = dht.readHumidity();

  // Read soil moisture
  int soilMoisture = analogRead(SOIL_MOISTURE_PIN);

  // Check if the values are valid (not NaN for DHT sensor)
  if (!isnan(temperature) && !isnan(humidity)) {
    // Define the path to the Firestore document
    String documentPath = "EspData/DHT11";

    // Create a FirebaseJson object for storing data
    FirebaseJson content;

    // Set the 'Temperature', 'Humidity', and 'SoilMoisture' fields in the FirebaseJson object
    content.set("fields/Temperature/doubleValue", temperature);
    content.set("fields/Humidity/doubleValue", humidity);
    content.set("fields/SoilMoisture/integerValue", soilMoisture);

    Serial.print("Update/Add Sensor Data... ");

    // Use the patchDocument method to update the Firestore document with the sensor data
    if (Firebase.Firestore.patchDocument(&fbdo, FIREBASE_PROJECT_ID, "", documentPath.c_str(), content.raw(), "Temperature,Humidity,SoilMoisture")) {
      Serial.printf("ok\n%s\n\n", fbdo.payload().c_str());
    } else {
      Serial.println(fbdo.errorReason());
    }
  } else {
    Serial.println("Failed to read DHT data.");
  }

  // Delay before the next reading
  delay(10000);
}
